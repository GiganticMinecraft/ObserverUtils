plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.0'
}

group = 'click.seichi'
version = '1.0.0'

sourceCompatibility = "1.11"
targetCompatibility = "1.11"

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'sk89q'
        url = 'https://maven.enginehub.org/repo/'
    }
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

dependencies {
    implementation 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    implementation 'com.sk89q.worldguard:worldguard-bukkit:7.0.2'
    implementation 'com.sk89q.worldedit:worldedit-bukkit:7.2.14'
    implementation 'com.sk89q.worldedit:worldedit-core:7.2.14'
    embed 'org.jetbrains.kotlin:kotlin-stdlib'
    embed 'com.google.code.gson:gson:2.10.1'
    embed 'com.squareup.okhttp3:okhttp:4.11.0'
    embed 'com.michael-bull.kotlin-result:kotlin-result:1.1.18'
    testImplementation 'io.kotest:kotest-runner-junit5-jvm:5.6.2'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}

test {
    useJUnitPlatform()
}
