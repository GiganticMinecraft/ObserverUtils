buildscript {
    ext.versions = [
        // Kotlinのバージョン指定はgradle.propertiesに
        spigot: '1.12.2-R0.1-SNAPSHOT',
        worldguard: '6.2'
    ]
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.guardsquare:proguard-gradle:7.1.0'
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlinVersion"
}

group = 'click.seichi'
version = '0.1.0'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'worldguard'
        url = 'https://maven.enginehub.org/repo/'
    }
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

dependencies {
    implementation "org.spigotmc:spigot-api:${versions.spigot}"
    implementation "com.sk89q.worldguard:worldguard-legacy:${versions.worldguard}"
    implementation 'com.github.shynixn.mccoroutine:mccoroutine-bukkit-api:1.5.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-core:1.3.2'
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    embed 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2'
    embed 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    embed 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.6.0'
    embed 'com.github.kittinunf.fuel:fuel:2.3.1'
    embed 'io.arrow-kt:arrow-core:1.0.1'
    embed 'com.github.shynixn.mccoroutine:mccoroutine-bukkit-core:1.5.0'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}

import proguard.gradle.ProGuardTask
task proguard(type: ProGuardTask) {
    injars "./build/libs/ObserverUtils-${version}.jar"
    outjars "./build/libs/ObserverUtils-${version}.opt.jar"
    libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
    libraryjars "${System.getProperty('user.home')}/.gradle/caches/modules-2/files-2.1/com.sk89q.worldguard/worldguard-legacy/${versions.worldguard}/4835055c94f69c8bba9857053828671f5f730308/worldguard-legacy-${versions.worldguard}.jar"
    libraryjars "${System.getProperty('user.home')}/.gradle/caches/modules-2/files-2.1/org.spigotmc/spigot-api/${versions.spigot}/a38cb171545121dc90ca6bcaf5072c4ec9bc9ca6/spigot-api-${versions.spigot}.jar"
    dontwarn 'kotlinx.coroutines.debug.AgentPremain'
    keep annotation: 'kotlinx.serialization.Serializable'
    keep annotation: 'kotlinx.serialization.SerialName'
    keep 'class click.seichi.observerutils.ObserverUtils'
    dontobfuscate
}
