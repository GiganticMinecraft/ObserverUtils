buildscript {
    ext.versions = [
            // Kotlinのバージョン指定はgradle.propertiesに
            spigot: '1.12.2-R0.1-SNAPSHOT',
            worldguard: '6.2',
            worldedit: '6.1.5'
    ]
    repositories {
        mavenCentral()
    }
}
plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
}

group = 'click.seichi'
version = '0.3.0'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'sk89q'
        url = 'https://maven.enginehub.org/repo/'
    }
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

dependencies {
    implementation "org.spigotmc:spigot-api:${versions.spigot}"
    implementation "com.sk89q.worldguard:worldguard-legacy:${versions.worldguard}"
    implementation "com.sk89q.worldedit:worldedit-bukkit:${versions.worldedit}"
    embed "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    embed 'com.google.code.gson:gson:2.9.0'
    embed 'com.squareup.okhttp3:okhttp:4.9.3'
    embed 'com.michael-bull.kotlin-result:kotlin-result:1.1.14'
    testImplementation 'io.kotest:kotest-runner-junit5-jvm:5.1.0'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}

test {
    useJUnitPlatform()
}
